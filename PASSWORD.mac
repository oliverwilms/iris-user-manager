ROUTINE PASSWORD [ type = mac]
PASSWORD 
#include %occInclude
CHECK(Username,Password) PUBLIC {
	; See if the password was previously used. If it was, return an error.
	; Allow the user to change it to the same one as current.
	; Store the list of previously used passwords for the user as a hashed value.
	; Prohibits password reuse for 10 generations
	Set tError = 0
	Set tErrorMsg = "Password does not meet requirements. Enter at least one uppercase, lowercase, numeric and special character. Minimum length is 12."
	If ((Password?.E1U.E)=0) Set tError = tError + 1
	If ((Password?.E1L.E)=0) Set tError = tError + 2
	If ((Password?.E1N.E)=0) Set tError = tError + 4
	If ((Password?.E1P.E)=0) Set tError = tError + 8
	If (tError > 0) Quit $$$ERROR($$$GeneralError,tErrorMsg)
	//Set PasswordHash = $System.Encryption.SHA1Hash(Password)
	Set tSalt = $System.Encryption.MD5Hash(Username)
	Set tHash = $System.Encryption.SHA1Hash(Password_tSalt)
	Set tHorolog = $Horolog
	If $Data(^PASSWORDLIST(Username,-1,tHash)) {
		Set tCurrent = $$CURRENT(Username)
		If ($Get(tCurrent)'=tHash) {
			Quit $$$ERROR($$$GeneralError,"Password was previously used")
		}
	}
	Set tDate = $Piece(tHorolog,",",1)
	Set tTime = $Piece(tHorolog,",",2)
	Set ^PASSWORDLIST(Username,-1,tHash) = tHorolog
	Set ^PASSWORDLIST(Username,tDate,tTime) = tHash
	; Clear password list
	#Define PasswordNotRepeatTimes 10
	Set tCount = 0
	Set tDate = ""
	For {
		Set tDate = $Order(^PASSWORDLIST(Username,tDate),-1)
		If (tDate = -1) Quit
		If (tDate = "") Quit
		Set tTime = ""
		For {
			Set tTime = $Order(^PASSWORDLIST(Username,tDate,tTime),-1)
			If (tTime="") Quit
			Set tCount = tCount + 1
			If (tCount > $$$PasswordNotRepeatTimes) {
				Set tHash = $Get(^PASSWORDLIST(Username,tDate,tTime))
				Kill ^PASSWORDLIST(Username,tDate,tTime)
				If (tHash '= "") Kill ^PASSWORDLIST(Username,-1,tHash)
			}
		}
	}
	Quit $$$OK
}
CURRENT(Username) PUBLIC {
	If (($Get(Username))="") Quit ""
	Set tDate = ""
	Set tDate = $Order(^PASSWORDLIST(Username,tDate),-1)
	If (tDate = -1) Quit ""
	If (tDate = "") Quit ""
	Set tTime = ""
	Set tTime = $Order(^PASSWORDLIST(Username,tDate,tTime),-1)
	If (tTime = "") Quit ""
	Quit $Get(^PASSWORDLIST(Username,tDate,tTime))
}
